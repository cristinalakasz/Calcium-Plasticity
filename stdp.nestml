"""
stdp - Synapse model for spike-timing dependent plasticity
#########################################################

Description
+++++++++++

stdp_synapse is a synapse with spike-timing dependent plasticity (as defined in [1]_). Here the weight dependence exponent can be set separately for potentiation and depression. Examples:

=================== ==== =============================
Multiplicative STDP [2]_ mu_plus = mu_minus = 1
Additive STDP       [3]_ mu_plus = mu_minus = 0
Guetig STDP         [1]_ mu_plus, mu_minus in [0, 1]
Van Rossum STDP     [4]_ mu_plus = 0 mu_minus = 1
=================== ==== =============================


References
++++++++++

.. [1] Guetig et al. (2003) Learning Input Correlations through Nonlinear
       Temporally Asymmetric Hebbian Plasticity. Journal of Neuroscience

.. [2] Rubin, J., Lee, D. and Sompolinsky, H. (2001). Equilibrium
       properties of temporally asymmetric Hebbian plasticity, PRL
       86,364-367

.. [3] Song, S., Miller, K. D. and Abbott, L. F. (2000). Competitive
       Hebbian learning through spike-timing-dependent synaptic
       plasticity,Nature Neuroscience 3:9,919--926

.. [4] van Rossum, M. C. W., Bi, G-Q and Turrigiano, G. G. (2000).
       Stable Hebbian learning from spike timing-dependent
       plasticity, Journal of Neuroscience, 20:23,8812--8821
"""

synapse stdp:

    ########################
    # STATE VARIABLES
    ########################
    state:
        # Long-term (structural) synaptic weight.
        # LTP/LTD is implemented in the neuron (via rho_GB -> g_AMPA_hat / Use),
        # If later move LTP/LTD to the synapse, update w here.
        w real = 1.0 @nest::weight

        # Tsodyks-Markram: fraction of available vescicles
        x real = 1.0
        
        # Tsodyks-Markram: utilization / release probability
        u real = 0.5
        u_tsyn real = u
        
        # Time of last presynaptic spike
        last_pre_spike ms = 0 ms
        
        # Debug / bookkeeping
        N_rel real = 0.0          # deterministic released vesicles count (currently not used downstream)
        test real = 0             # currently used to mantain onReceive(post_spikes) without raising errors
        
        
    ########################
    # PARAMETERS
    ########################
    parameters:
        d ms = 1 ms @nest::delay  # transmission delay
        
        # (Unused placeholders from legacy STDP template)
        Wmax real = 100.0
        Wmin real = 0.0
        
        # TM time constants
        tau_rec ms = 800 ms       # resource recovery time constant
        tau_fac ms = 10 ms        # facilitation decay time constant
        
        # Vesicle release sites (only used to compute N_rel for future use)
        N_sites real = 5
        
        
    ########################
    # CONTINUOUS DYNAMICS
    ########################
    equations:
        # Recovery of available resources between spikes
        x' = (1.0 - x) / tau_rec


    ########################
    # I/O
    ########################
    input:
        pre_spikes <- spike
        post_spikes <- spike
        Use_in real <- continuous

    output:
        spike

    
    ########################
    # POSTSYNAPTIC SPIKES
    ########################
    onReceive(post_spikes):
        # Currently not used for plasticity
        test += 1
        
    
    ########################
    # PRESYNAPTIC SPIKES
    ########################
    onReceive(pre_spikes):
        # Inter-spike interval
        inline delta ms = t - last_pre_spike
        if delta < 0 ms:
            delta = 0 ms
            
        # TM facilitation update:
        # Use_in acts as the baseline utilization towards which u relaxes
        u = u_tsyn * exp( - (delta)/tau_fac) + Use_in*(1 - u_tsyn*exp( - (delta)/tau_fac))
        
        # Released fraction for this spike (before depletion)
        inline rel real = u * x
        
        # Optional deterministic vesicle count (not used yet)
        N_rel = N_sites * rel
        
        # Effective synaptic efficacy delivered on this spike
        inline w_eff real = w * rel
        deliver_spike(w_eff, d)
        
        # resource depletion at spike time
        x = x - rel
        if x < 0.0:
            x = 0.0
            
        # Update state for next spike
        last_pre_spike = t
        u_tsyn = u
        


